// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero the input, if zx or zy are true
    Mux16(a = x, b[0..15] = false, sel = zx, out = x1);
    Mux16(a = y, b[0..15] = false, sel = zy, out = y1);

    // Negate the inputs if nx or ny are true
    Not16(in = x1, out = nx1);
    Not16(in = y1, out = ny1);
    Mux16(a = x1, b = nx1, sel = nx, out = x2);
    Mux16(a = y1, b = ny1, sel = ny, out = y2);

    // Perform the requested operation
    And16(a = x2, b = y2, out = andout);
    Add16(a = x2, b = y2, out = addout);
    Mux16(a = andout, b = addout, sel = f, out = out1);

    // Negate the output if no is true
    Not16(in = out1, out = nout);
    Mux16(a = out1, b = nout, sel = no, out = outfinal, out = out);

    // Set the zr outputs
    And16(a = outfinal, b[0..7] = true, b[8..15] = false, out[0..7] = out07);
    And16(a = outfinal, b[0..7] = false, b[8..15] = true, out[8..15] = out815);
    Or8Way(in = out07, out = zr1);
    Or8Way(in = out815, out = zr2);
    Or(a = zr1, b = zr2, out = zr3);
    Not(in = zr3, out = zr);

    // Set the ng outputs
    And16(a = outfinal, b[0..15] = true, out[15] = high15);
    And(a = high15, b = true, out = ng);
    
}